grammar org.openmodelica.modelicaml.editor.xtext.algorithm.Algorithmsection 
	with org.openmodelica.modelicaml.editor.xtext.Modeleditor

generate algorithmsection "http://www.openmodelica.org/modelicaml/editor/xtext/algorithm/Algorithmsection"
//////////////////////////////////////////Algorithm//////////////////////
algorithm_clause:{algorithm_clause}
(Algorithm+=statement ';')* 
;
statement :{statement}
(If_statement=if_statement
| State_OutputExprList=state_OutputExprList
| Algo_M_G=Algorithm_Macros_GEN_SIGNAL
| 'return'
| state_Comp_Ref=state_Comp_Ref 
| For_statement=for_statement
| When_statement=when_statement
| While_statement=while_statement 
| Algo_G_I=Algorithm_Macros_GEN_INC
| Algo_G_C=Algorithm_Macros_GEN_CHANGE
| 'break'
)
comment
;
///////////////////////////////////Macros//////////////////////////////////
Algorithm_Macros_GEN_SIGNAL:
'GEN_SIGNAL' '('Component_reference=component_reference')'
;
Algorithm_Macros_GEN_INC:
'GEN_INC' '('Component_reference=component_reference')'
;
Algorithm_Macros_GEN_CHANGE:
'GEN_CHANGE' '('Component_reference=component_reference')'
;
///////////////////////////////////Statements//////////////////////////////
state_OutputExprList:
'(' output_expression_list=output_expression_list ')' ':=' Component_ref=component_reference fun_call_args=function_call_args
;

state_Comp_Ref:
component_reference=component_reference ( ':=' expr=expression | fun_call_args=function_call_args ) 
;
when_statement :
'when' When_expr=expression 'then'
( When_stat_true+=statement ';' )*
( 'elsewhen' Else_When_expr+=expression 'then'
( Then_statement+=statement ';' )* )*
'end''when'
;
while_statement :
'while' while_expr=expression 'loop'
( While_stat+=statement ';' )*
'end''while';

for_statement :
'for' for_loop=for_indices 'loop'
( For_algo+=statement ';')*
'end' 'for'
;
if_statement :
'if' exprtrue=expression 'then'( state+=statement ';')*
('elseif' exprStilltrue+=expression 'then'( state+=statement ';' )*)*
( 'else'( state+=statement';' )*)?
'end''if'
;